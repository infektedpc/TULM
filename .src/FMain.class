' Gambas class file

Public SelectedImage As String = ""
Public Categories As String = ""
Public OnlyShowIn As String = ""
'Messages
Public MSG_TULM_Only As String = "TULM can only open launchers created with TULM."
Public MSG_Desktop_File_Exist As String = "File already exist, Replace file?"
Public MSG_No_File_Name As String = "Launcher has not been given a name."
Public MSG_Unsupported_Image_Type As String = "Please select a supported image type."
Public MSG_No_Catagory_To_Add As String = "Please select a catagory to add."
Public MSG_No_Catagory_To_Remove As String = "Please select a catagory to remove."
Public MSG_No_Categories As String = "Please add at least one catagory."
Public MSG_No_EXEC As String = "Launcher has not been given a executable path."
'Checkbox Values
Public Unity_CheckBox As Boolean = False
Public Cinnamon_CheckBox As Boolean = False
Public KDE_CheckBox As Boolean = False
Public Gnome_CheckBox As Boolean = False
Public RazorQT_CheckBox As Boolean = False
Public MATE_CheckBox As Boolean = False
Public XFCE_CheckBox As Boolean = False
Public Pantheon_CheckBox As Boolean = False
Public LXDE_CheckBox As Boolean = False
Public All_CheckBox As Boolean = True

Public Sub _new()
  
End

Public Sub Form_Open()
  'Center Form
  
  FMain.Center
  'Set default image Path for ....
  SelectedImage = "/usr/share/pixmaps/tulm.png" 'ADD PATH HERE
  PictureBox1.Picture = picture.Load(SelectedImage)
  
End

Public Sub Button_Open_Click()
  
  Open_Desktop_File
  
End

Public Sub Open_Desktop_File()
  
  Dim Temp_File As String[]
  Dim Temp_File_Line As String = ""
  Dim Temp_Categories As String[]
  Dim Temp_Temp_Categories_Line As String = ""
  
  'Open File Dialog
  Dialog.Title = "Open .Desktop File"
  Dialog.filter = ["*All Files"]
  'Set Default Path
  Dialog.Path = "/home/" & System.User.Name & "/.local/share/applications"
  If Dialog.OpenFile(False) Then Return
  'Check For TULM Comment
  If InStr(file.Load(Dialog.path), "#Created With Tulm 0.1") = 0 Then
    Message.Info(MSG_TULM_Only)
  Else
    'Clear Categories
    ListBox_Categories.Clear
    
    'Split File into array
    Temp_File = Split(file.Load(Dialog.path), "\n", Null, True, False)
    
    'Go Through "Temp File" Line by Line
    For Each Temp_File_Line In Temp_File
      
      'Check For Name
      If InStr(Temp_File_Line, "Name") = 1 Then
        TextBox_Name.text = Mid$(Temp_File_Line, 6)
      Endif
      
      'Check for Comment
      If InStr(Temp_File_Line, "Comment") = 1 Then
        TextBox_Comment.text = Mid$(Temp_File_Line, 9)
      Endif
      
      'Check for Exec
      If InStr(Temp_File_Line, "Exec") = 1 Then
        TextBox_EXEC.text = Mid$(Temp_File_Line, 6)
      Endif
      
      'Check for Icon
      If InStr(Temp_File_Line, "Icon") = 1 Then
        SelectedImage = Mid$(Temp_File_Line, 6)
        If Exist(SelectedImage) Then
          PictureBox1.picture = picture.Load(SelectedImage)
        Else
          'RELOAD THE DEFALT PICTURE HERE
          SelectedImage = "/usr/share/pixmaps/tulm.png" 'ADD PATH HERE
          PictureBox1.Picture = picture.Load(SelectedImage)
        Endif
      Endif
      
      'Check For Categories
      If InStr(Temp_File_Line, "Categories") = 1 Then
        Temp_Categories = Split(Mid$(Temp_File_Line, 12), ";", Null, True, True)
        For Each Temp_File_Line In Temp_Categories
          ListBox_Categories.Add(Temp_File_Line)
        Next
      Endif
      
      'Check For Terminal
      If InStr(Temp_File_Line, "Terminal") = 1 Then
        If Mid$(Temp_File_Line, 10) = "false"
          CheckBox_Terminal.Value = False
        Else
          CheckBox_Terminal.Value = True
        Endif
      Endif
      
      'Check For StartupNotify
      If InStr(Temp_File_Line, "StartupNotify") = 1 Then
        If Mid$(Temp_File_Line, 15) = "false"
          CheckBox_StartupNotify.Value = False
        Else
          CheckBox_StartupNotify.Value = True
        Endif
      Endif
      
    Next
    'Clean
    Temp_File.Clear
  Catch
    Message.Error("Error: " & Dialog.Path & "\n" & Error.Text)
  Endif
  
End

Public Sub Button_Save_Click()
  
  If Not TextBox_Name.Text = "" Then
    If Not TextBox_EXEC.Text = "" Then
      If Not ListBox_Categories.Count = -1 Then
        'Check if file Exist
        If Exist("/home/" & System.User.Name & "/.local/share/applications/" & TextBox_Name.Text & ".desktop") Then
          If Message.Question(MSG_Desktop_File_Exist, "Yes", "No") = 1 Then
            Create_Desktop_File
          Endif
        Else
          Message.Info(MSG_No_Categories)
        Endif
      Else
        Create_Desktop_File
      Endif
    Else
      Message.Info(MSG_No_EXEC)
    Endif
  Else
    Message.Info(MSG_No_File_Name)
  Endif
  
End

Public Sub Create_Desktop_File()
  
  Dim StartupNotify_Boolean As Boolean = CheckBox_StartupNotify.Value
  Dim Terminal_Boolean As Boolean = CheckBox_Terminal.Value
  Dim Temp_File_Name As String = Lower$(TextBox_Name.Text)
  Dim InvalChar As String 
  Dim InvalidChars As String[] = [Chr$(92), "/", ":", "*", "?", Chr$(34), "<", ">", "|", Chr$(32)] 
  
  'Remove InvalidChars from Temp_File_Name
  For Each InvalChar In InvalidChars 
    Temp_File_Name = Replace(Temp_File_Name, InvalChar, Null) 
  Next 
  
  'String Categories From ListBox_Categories
  Categories = ListBox_Categories.List.Join(";")
  
  If Unity_CheckBox = False And If Cinnamon_CheckBox = False And If KDE_CheckBox = False And If Gnome_CheckBox = False And If RazorQT_CheckBox = False And If MATE_CheckBox = False And If XFCE_CheckBox = False And If Pantheon_CheckBox = False And If LXDE_CheckBox = False Then
    
    'Save .desktop
    file.Save("/home/" & System.User.Name & "/.local/share/applications/" & Temp_File_Name & ".desktop", "#Created With Tulm 0.1\n" & "[Desktop Entry]\n" & "Version=1.0\n" & "Name=" & TextBox_Name.text & "\n" & "Comment=" & TextBox_Comment.text & "\n" & "Exec=" & TextBox_EXEC.Text & "\n" & "Icon=" & SelectedImage & "\n" & "Categories=" & Categories & "\n" & "Terminal=" & Str$(Terminal_Boolean) & "\n" & "Type=Application" & "\n" & "StartupNotify=" & Str$(StartupNotify_Boolean))
    
  Else
    'Unity
    If Unity_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_Unity.Text & ";"
    Endif
    'Cinnamon
    If Cinnamon_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_Cinnamon.Text & ";"
    Endif
    'KDE
    If KDE_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_KDE.Text & ";"
    Endif
    'Gnome
    If Gnome_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_Gnome.Text & ";"
    Endif
    'RazorQT
    If RazorQT_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_RazorQT.Text & ";"
    Endif
    'MATE
    If MATE_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_MATE.Text & ";"
    Endif
    'XFCE
    If XFCE_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_XFCE.Text & ";"
    Endif
    'Pantheon
    If Pantheon_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_Pantheon.Text & ";"
    Endif
    'LXDE
    If LXDE_CheckBox = True Then
      OnlyShowIn = OnlyShowIn & FOnly.CheckBox_LXDE.Text & ";"
    Endif
    
    'Save .desktop
    file.Save("/home/" & System.User.Name & "/.local/share/applications/" & Temp_File_Name & ".desktop", "#Created With Tulm 0.1\n" & "[Desktop Entry]\n" & "Version=1.0\n" & "Name=" & TextBox_Name.text & "\n" & "Comment=" & TextBox_Comment.text & "\n" & "Exec=" & TextBox_EXEC.Text & "\n" & "Icon=" & SelectedImage & "\n" & "Categories=" & Categories & "\n" & "Terminal=" & Str$(Terminal_Boolean) & "\n" & "Type=Application" & "\n" & "StartupNotify=" & Str$(StartupNotify_Boolean) & "\n" & "OnlyShowIn=" & Str$(OnlyShowIn))
    
  Endif
  
  'File Save MSG
  Message.Info("File " & TextBox_Name.Text & ".desktop Created!")
  
  'Clean
  SelectedImage = "/usr/share/pixmaps/tulm.png"
  PictureBox1.Picture = picture.Load(SelectedImage)
  Categories = ""
  TextBox_Name.Clear
  TextBox_Comment.Clear
  TextBox_EXEC.Clear
  ComboBox_Categories.text = ""
  ListBox_Categories.Clear
  CheckBox_StartupNotify.Value = False
  CheckBox_Terminal.Value = False
  'False all Checkbox values
  Unity_CheckBox = False
  Cinnamon_CheckBox = False
  KDE_CheckBox = False
  Gnome_CheckBox = False
  RazorQT_CheckBox = False
  MATE_CheckBox = False
  XFCE_CheckBox = False
  Pantheon_CheckBox = False
  LXDE_CheckBox = False
  All_CheckBox = False
  
Catch
  Message.Error("Error: " & Error.Text)
  
End

Public Sub PictureBox1_MouseDown()
  'Open File Dialog
  Dialog.Title = "Select Image"
  Dialog.filter = ["*.jpg;*.jpeg;*.png;*.bmp", "All Files", "*.jpg;*.jpeg", "Joint Photographic Expert Group", "*.png", "Portable Network Graphics", "*.bmp", "Bitmap"]
  'Set Default Path
  If Exist("/home/" & System.User.Name & "/Pictures") Then
    Dialog.Path = "/home/" & System.User.Name & "/Pictures"
  Endif
  If Dialog.OpenFile(False) Then Return
  SelectedImage = ""
  'Check File Ext
  If file.ext(Dialog.path) = "jpg" Or file.ext(Dialog.path) = "jpeg" Or file.ext(Dialog.path) = "png" Or file.ext(Dialog.path) = "bmp" Then
    PictureBox1.picture = picture.Load(Dialog.path)
    SelectedImage = Dialog.path
  Else
    Message.info(MSG_Unsupported_Image_Type)
  Endif
Catch
  Message.Error("Error: " & Dialog.path & "\n" & Error.Text)
  
End

Public Sub Button_EXEC_Click()
  
  'Open File Dialog
  Dialog.Title = "Select File"
  Dialog.filter = [""]
  'Set Default Path
  If Exist("/home/" & System.User.Name) Then
    Dialog.Path = "/home/" & System.User.Name
  Endif
  If Dialog.OpenFile(False) Then Return
  TextBox_Name.text = File.BaseName(Dialog.Path)
  TextBox_EXEC.text = Dialog.path
Catch
  Message.Error("Error: " & Dialog.path & "\n" & Error.Text)
  
End

Public Sub Button_Add_Category_Click()
  
  If Not ComboBox_Categories.Text = "" Then
    ListBox_Categories.Add(ComboBox_Categories.Text)
    ComboBox_Categories.Text = ""
  Else
    Message.Info(MSG_No_Catagory_To_Add)
  Endif
  
End

Public Sub Button_Remove_Category_Click()
  
  If Not ListBox_Categories.Index = -1 Then
    ListBox_Categories.Remove(ListBox_Categories.Index)
  Else
    Message.Info(MSG_No_Catagory_To_Remove)
  Endif
  
End

Public Sub MenuFile_Open_Click()
  
  Open_Desktop_File
  
End

Public Sub MenuFile_Save_Click()
  
  If Not TextBox_Name.Text = "" Then
    'Check if file Exist
    If Exist("/home/" & System.User.Name & "/.local/share/applications/" & TextBox_Name.Text & ".desktop") Then
      If Message.Question(MSG_Desktop_File_Exist, "Yes", "No") = 1 Then
        Create_Desktop_File
      Endif
    Else
      Create_Desktop_File
    Endif
  Else
    Message.Info(MSG_No_File_Name)
  Endif
  
End

Public Sub MenuAbout_Help_Click()
  
  FHelp.Show
  
End

Public Sub MenuAbout_About_Click()
  
  FAbout.show
  
End

Public Sub Button_Desktops_Click()  
  
  FOnly.ShowDialog
  
End
